// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.10'
    ext.dexcount_plugin_version = '0.8.3'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:$dexcount_plugin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.0.0.RC6-3")
}

def versionProperties = new Properties()
file("version.properties").withInputStream { versionProperties.load(it) }
project.ext.versionMajor = versionProperties.getProperty("versionMajor")
project.ext.versionMinor = versionProperties.getProperty("versionMinor")
project.ext.versionPatch = versionProperties.getProperty("versionPatch")
project.ext.versionCode = versionProperties.getProperty("versionCode")
project.ext.versionName = "${project.ext.versionMajor}.${project.ext.versionMinor}.${project.ext.versionPatch}"

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

detekt {
    version = "1.0.0.RC6-3"
    profile("main") {
        config = "$projectDir/default-detekt-config.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}

apply from: 'applinks.gradle'
apply from: 'dependencies.gradle'
